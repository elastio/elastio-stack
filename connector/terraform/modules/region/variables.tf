#########################
## Required parameters ##
#########################

variable "elastio_pat" {
  description = "Personal Access Token generated by the Elastio Portal"
  sensitive   = true
  type        = string
  nullable    = false
}

variable "elastio_tenant" {
  description = "Name of your Elastio tenant. For example `mycompany.app.elastio.com`"
  type        = string
  nullable    = false
}

variable "connector_account_stack" {
  description = <<DESCR
    The Elastio Connector Account stack metadata. This is used to inherit the
    configs by the `region` stack. The value for this parameter can be
    provided as the `cloudformation_stack` output of the `account` module, or
    you could use a `data "aws_cloudformation_stack"` data source to fetch the
    stack metadata and provide it here.
  DESCR

  type = object({
    name       = string
    parameters = map(string)
  })

  nullable = false
}

#########################
## Optional parameters ##
#########################

variable "region" {
  description = <<DESCR
    The AWS region where the Elastio connector will be deployed. If not specified
    then the currently configured default region will be used.
  DESCR

  type    = string
  default = null
}

variable "vpc_id" {
  description = <<DESCR
    The ID of the VPC where the Elastio connector will be deployed.

    Must be omitted if the `network_configuration` was set to `Auto` in the `account` module,
    in which case Elastio will automatically create a VPC and subnets.
  DESCR

  type    = string
  default = null

  validation {
    condition = (
      var.vpc_id == null ? local.network_configuration == "Auto" : true
    )

    error_message = <<MSG
      The `vpc_id` must be omitted if the `network_configuration`
      was set to `Auto` in the Connector Account stack.
    MSG
  }

  validation {
    condition = (
      var.vpc_id != null ? local.network_configuration == "Manual" : true
    )

    error_message = <<-MSG
      The `vpc_id` must be provided if the `network_configuration`
      was set to `Manual` in the Connector Account stack.
    MSG
  }
}

variable "subnet_ids" {
  description = <<DESCR
    The IDs of the subnets where the Elastio connector will be deployed

    Must be omitted if the `network_configuration` was set to `Auto` in the `account` module,
    in which case Elastio will automatically create a VPC and subnets.
  DESCR

  type    = list(string)
  default = null

  validation {
    condition = (
      length(coalesce(var.subnet_ids, [])) == 0 ? local.network_configuration == "Auto" : true
    )

    error_message = <<MSG
      The `subnet_ids` must be omitted if the `network_configuration`
      was set to `Auto` in the Connector Account stack.
    MSG
  }

  validation {
    condition = (
      length(coalesce(var.subnet_ids, [])) > 0 ? local.network_configuration == "Manual" : true
    )

    error_message = <<-MSG
      The `subnet_ids` must be provided if the `network_configuration`
      was set to `Manual` in the Connector Account stack.
    MSG
  }
}

locals {
  network_configuration = var.connector_account_stack.parameters["networkConfiguration"]
}
